apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "de.fhbielefeld.githubstatsgraphql"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "GITHUB_USERNAME", "\"" +
                    getFromSecrets('GITHUB_USERNAME') + "\""
            variant.buildConfigField "String", "GITHUB_PASSWORD", "\"" +
                    getFromSecrets('GITHUB_PASSWORD') + "\""
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.moshi:moshi:1.2.0'
}

def getFromSecrets(String key) {
    def Properties props = new Properties()

    try {
        props.load(new FileInputStream(new File('secrets.properties')))
    } catch (Exception ignored) {
        throw new GradleException('Please add a secrets.properties file with your data.')
    }

    if (props.containsKey(key)) {
        return props[key]
    } else {
        throw new GradleException('Please add a ' + key + ' value to your secrets.properties file.')
    }
}
